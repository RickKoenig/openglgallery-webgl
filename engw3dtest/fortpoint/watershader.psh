// c0		- eye pos

ps_2_0

def c30, 2.0, 16.0, 2.0, 0.0
def c31, 0.5, 2.0, 0.0, 1.0

dcl t0.xy					// Tex coord
dcl t1.xy					// Tex coord
dcl t2						// World space pos

dcl_2d s0					// Texture map
dcl_cube s1					// Reflect map

texld r0, t0, s0			// Sample bump texture
texld r1, t1, s0			// Sample bump texture
mad r0, r0, c31.y, -c31.w
mad r1, r1, c31.y, -c31.w
add r0, r0, r1

// scale bumps and renormalize (shouldn't need this with proper texture)
mul r1, r0, c30
nrm_pp r0, r1

sub r2, c0, t2
nrm_pp r1, r2

// reflect
dp3 r2, r0, r1
mul r2, r2, c31.y
mad r2, r2, r0, -r1

// sample cubemap
texld r3, r2, s1

// pseudo-fresnel darkening
mad r3.xyz, r3, -r2.y, r3

mov oC0, r3
